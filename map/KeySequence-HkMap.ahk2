#Requires AutoHotkey v2.0-

; #Include %A_ScriptDir%\..\lib\Script.ahk2
; #Include %A_ScriptDir%\..\map\HkMap.ahk2

class KeySequenceHk extends HkMap {
    static TabSize => 2

    static GetMarkdownToSlackSequence(markdown, doubleSpace := true) {
        lines := StrSplit(markdown, "`r`n")
        prevLevel := 0
        seq := ""
        isList := false

        for _, line in lines {
            level := 0

            if line ~= "^\s*$"
                continue

            if not (line ~= "^\s*-\s") {
                if isList {
                    seq .= "^+8"
                    isList := false
                }

                seq .= line . "+{Enter}"
                continue
            }

            if not isList {
                seq .= "^+8"
                isList := true
            }

            spaceLen := 0

            loop parse (line) {
                if (A_LoopField != A_Space)
                    break

                spaceLen := spaceLen + 1
            }

            level := spaceLen / KeySequenceHk.TabSize

            if doubleSpace and level = 0 {
                seq .= "+{Enter}+{Enter}^+8"
            }

            seq .= RegExReplace(line, "^\s*-\s*", "")
            diff := level - prevLevel

            if diff != 0 {
                seq .= "+{Home}"
            }

            index := diff

            while index < 0 {
                seq .= "+{Tab}"
                index := index + 1
            }

            while index > 0 {
                seq .= "{Tab}"
                index := index - 1
            }

            if diff != 0 {
                seq .= "{End}"
            }

            seq .= "+{Enter}"
            prevLevel := level
        }

        if isList {
            seq .= "^+8"
        }

        return seq
    }

    static AddTable(&table) {
        table["md-to-slack"] := [
            "Convert a Markdown tree on the Clipboard "
            "to a message post sequence on Slack. "
            "Note: Only works when the length of each "
            "line does not cause word wrap. To assure "
            "this, limit the length of each line, and "
            "zoom out as much as possible in the "
            "Slack app in order to shrink the text.",
            () => Script.Send(
                KeySequenceHk.GetMarkdownToSlackSequence(
                    A_Clipboard,
                    true  ; doubleSpace
                )
            )
        ]
    }
}

KeySequenceHk().Main((t) => KeySequenceHk.AddTable(&t))

:*?:;md-to-slack;::
{
    KeySequenceHk().CallThisKey()
}
